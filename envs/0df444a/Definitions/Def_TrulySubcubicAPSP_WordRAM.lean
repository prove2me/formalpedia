-- Prove2me | Definitions.Def_TrulySubcubicAPSP_WordRAM
-- name    : TrulySubcubicAPSP_WordRAM
-- status  : Definition
-- author  : @wurtle
-- created : 2026-10-06T04:48:25.356988+00:00
-- url     : https://prove2.me/theorems/3bd23fd0-c815-4290-8bf1-05bbdefe7e89
-- title:
--   Word RAM execution and uniform polynomial running time
-- statement:
--   A deterministic random-access machine has integer-addressed memory of $W$-bit words and a finite instruction list. The instructions write one; add, subtract, or multiply modulo $2^W$; load or store indirectly using signed addresses; branch on a negative signed value; accept; or reject. Every instruction, including halting, costs one step. The input starts with its size in cell zero, all unused cells initially contain zero, and output follows the input.
--
--   For a problem $Q$ and positive rational $r=a/d$ in lowest terms, the uniform running-time specification requires, for every $\kappa\in\mathbb N$, a single program $P$, a constant $b\in\mathbb N$, and $T:\mathbb N\to\mathbb N$ such that
--   $$\exists K\in\mathbb N\;\forall n\ge2,\qquad T(n)^d\le K n^a,$$
--   and every size-$n$ input with all encoded entries bounded in absolute value by $n^\kappa$ is answered correctly within $T(n)$ steps for every $W\ge b(\operatorname{Nat.log2}(n)+1)$. Correctness includes size zero and one; the rational asymptotic predicate uses the natural part of the numerator for other rational arguments. The answer consists of the required acceptance verdict and signed output values. This layer supplies the shared operational meaning of all three running-time targets.
-- source:
--   https://github.com/anthropics/formal-math/blob/e1a4e6508154ea59f030480661590a9fe3018011/3sum-apsp/EndStatement.lean; lines 15–73; namespace renamed only; Apache-2.0.

/-
Copyright (c) 2026 Anthropic, PBC. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
SPDX-License-Identifier: Apache-2.0
-/

set_option autoImplicit false
set_option relaxedAutoImplicit false

namespace TrulySubcubicAPSP

/-- `i j k` name cells, `l` a position in the program, `[i]` is the word in cell `i`. The only constant is 1. -/
inductive Instr where
  | one (i : Int)             -- [i] := 1
  | add (i j k : Int)         -- [i] := [j] + [k]
  | sub (i j k : Int)         -- [i] := [j] - [k]
  | mul (i j k : Int)         -- [i] := [j] * [k]
  | load (i j : Int)          -- [i] := [[j]]
  | store (i j : Int)         -- [[i]] := [j]
  | bltz (i : Int) (l : Nat)  -- if [i] < 0, go to position l
  | accept
  | reject

/-- The verdict and the final memory, if `P`, run from position `pc` on memory `m`, halts within `t` steps, the halting
step counted; past its end `P` rejects. Addresses are read signed; `bltz` jumps on a negative word; `write` runs on. -/
def exec {W : Nat} (P : List Instr) : (t pc : Nat) → (m : Int → BitVec W) → Option (Bool × (Int → BitVec W))
  | 0, _, _ => none
  | t + 1, pc, m =>
    let write (i : Int) (v : BitVec W) := exec P t (pc + 1) fun x => if x = i then v else m x
    match P.getD pc .reject with
    | .one i => write i 1
    | .add i j k => write i (m j + m k)
    | .sub i j k => write i (m j - m k)
    | .mul i j k => write i (m j * m k)
    | .load i j => write i (m (m j).toInt)
    | .store i j => write (m i).toInt (m j)
    | .bltz i l => exec P t (if (m i).toInt < 0 then l else pc + 1) m
    | .accept => some (true, m)
    | .reject => some (false, m)

/-- The memory at the start: `ws` in cells 0, 1, 2, …, and 0 in every other cell. -/
def loadWords (W : Nat) (ws : List Int) : Int → BitVec W :=
  fun a => if a < 0 then 0 else BitVec.ofInt W (ws.getD a.toNat 0)

/-- `T(n) = O(n^r)`, both sides raised to the power `r.den`: core Lean has no fractional powers. For `r = 1.9992 =
2499/1250` it says `T(n)^1250 ≤ K n^2499`. -/
def BigO (T : Nat → Nat) (r : Rat) : Prop :=
  ∃ K : Nat, ∀ n ≥ 2, T n ^ r.den ≤ K * n ^ r.num.toNat

/-- `yes`: when to accept (always, if not given). `output`: a condition on the cells after the input, read as signed. -/
structure Problem where
  Instance : Nat → Type
  input {n : Nat} : Instance n → List Int
  yes {n : Nat} : Instance n → Prop := fun _ => True
  output {n : Nat} : Instance n → (Nat → Int) → Prop := fun _ _ => True

/-- One run: with `n` in cell 0 and the input after it, `P` halts within `t` steps with the right verdict and output. -/
def Problem.SolvedBy (Q : Problem) {n : Nat} (x : Q.Instance n) (P : List Instr) (W t : Nat) : Prop :=
  ∃ verdict m, exec P t 0 (loadWords W ((n : Int) :: Q.input x)) = some (verdict, m) ∧
    (verdict = true ↔ Q.yes x) ∧ Q.output x fun a => (m (1 + (Q.input x).length + a : Nat)).toInt

/-- Theorem 2: «a word RAM with O(log n)-bit words», «all numbers in the input are integers of absolute value
n^O(1)»: for every `κ`, one `P`, `b`, `T` for all instances, correct at every `W ≥ b(⌊log₂ n⌋ + 1)`. -/
def Problem.SolvedInTime (Q : Problem) (r : Rat) : Prop :=
  ∀ κ : Nat, ∃ (P : List Instr) (b : Nat) (T : Nat → Nat), BigO T r ∧
    ∀ (n : Nat) (x : Q.Instance n), (∀ a ∈ Q.input x, a.natAbs ≤ n ^ κ) → ∀ W ≥ b * (Nat.log2 n + 1),
      Q.SolvedBy x P W (T n)

def rowByRow {n : Nat} (w : Fin n → Fin n → Int) : List Int :=
  (List.ofFn fun u => List.ofFn fun v => w u v).flatten

end TrulySubcubicAPSP


